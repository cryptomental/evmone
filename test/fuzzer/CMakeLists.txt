# evmone: Fast Ethereum Virtual Machine implementation
# Copyright 2019 The evmone Authors.
# Licensed under the Apache License, Version 2.0.
include(ExternalProject)

get_target_property(type evmone TYPE)
if(NOT type STREQUAL STATIC_LIBRARY)
    message(FATAL_ERROR "The evmone must be built as static library")
endif()

if(EVMONE_FUZZING_COVERAGE)
    set(CMAKE_EXE_LINKER_FLAGS "-fprofile-instr-generate -fcoverage-mapping -fsanitize=fuzzer")
else()
    string(REPLACE fuzzer-no-link fuzzer CMAKE_EXE_LINKER_FLAGS ${CMAKE_EXE_LINKER_FLAGS})
endif()

add_executable(evmone-fuzzer fuzzer.cpp)
target_link_libraries(evmone-fuzzer PRIVATE evmone testutils)

if(NOT EVMONE_FUZZING_COVERAGE)
    ExternalProject_Add(
        aleth-interpreter
        PREFIX external
        EXCLUDE_FROM_ALL TRUE

        GIT_REPOSITORY https://github.com/ethereum/aleth
        GIT_TAG aleth-interpreter
        GIT_SUBMODULES evmc cmake/cable
        GIT_SHALLOW TRUE
        UPDATE_DISCONNECTED TRUE

        CMAKE_ARGS
            -DCMAKE_BUILD_TYPE=Release
            -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
            -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
            -DCMAKE_CXX_FLAGS=${fuzzing_flags}
            -DCMAKE_C_FLAGS=${fuzzing_flags}

        BUILD_COMMAND ${CMAKE_COMMAND} --build <BINARY_DIR> --target aleth-interpreter
        INSTALL_COMMAND ""
    )

    set(build_dir ${CMAKE_CURRENT_BINARY_DIR}/external/src/aleth-interpreter-build)

    add_library(aleth::interpreter IMPORTED STATIC)
    add_dependencies(aleth::interpreter aleth-interpreter)
    set_target_properties(aleth::interpreter PROPERTIES
        IMPORTED_CONFIGURATIONS Release
        IMPORTED_LOCATION_RELEASE ${build_dir}/libaleth-interpreter/libaleth-interpreter.a
        INTERFACE_LINK_LIBRARIES ${build_dir}/aleth/libaleth-buildinfo.a)

    target_link_libraries(evmone-fuzzer PRIVATE aleth::interpreter)
    target_compile_definitions(evmone-fuzzer PRIVATE ALETH)
endif()